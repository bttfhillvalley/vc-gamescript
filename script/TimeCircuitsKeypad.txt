// 1@ - Keystrokes counter
// 2@ - Number
// 3@-4@ - Key pressed
// 5@ - MONTH
// 6@ - DAY
// 7@ - YEAR
// 8@ - HOUR
// 9@ - MINUTE
// 10@ - ErrorCheck
// 11@ - 15@ Free
:TimeCircuitsKeypad
wait 10
if
    Car.Wrecked(0@)
then
    end_thread
end
if
    00DC:   player $PLAYER_CHAR in_car 0@
else_jump @TimeCircuitsKeypad
008B: 14@ = $TCON
008B: 15@ = $FRIED

// Time Circuits On/Off
if
    0AB0: key_pressed 0x6B
then
    gosub @TimeCircuitsToggle
end

// Keypad stuff
for 2@ = 0 to 9 step 1
    0B11: 3@ = 2@ OR 0x30
    0B11: 4@ = 2@ OR 0x60
    if or
        0AB0: key_pressed 3@
        0AB0: key_pressed 4@
    then
        gosub @TimeCircuitsKeyPressed
    end
end

// Enter Key
if or
    0AB0: key_pressed 0x6D
    0AB0: key_pressed 0xBD
then
    gosub @TimeCircuitsKeypadConfirm
end
jump @TimeCircuitsKeypad

// =========================================================================================

:TimeCircuitsToggle
if
    $TCON == 0
then
    $TCON = 0xF0
    3F84: play_sound "startup.wav" loop 0
else
    $TCON = 0
    3F84: play_sound "shutdown.wav" loop 0
end
// Give time for time circuits to turn on
wait 500
while 0AB0: key_pressed 0x6B
    wait 10
end
return
// =========================================================================================

:TimeCircuitsKeyPressed
if and
    not 14@ == 0
    15@ == 0
then
    3F90: play_keypad_sound 2@
    if
        1@ < 8
    then
        7@ *= 10
        005A: 7@ += 2@
        1@ += 1
    else if
        1@ < 12
    then
        9@ *= 10
        005A: 9@ += 2@
        1@ += 1
    end
    end
end
3F15: move_car_part "tcdkeypadbutton" index 2@ pos 0.0 0.005 0.0 car 0@

:TimeCircuitsKeyHold
wait 10
if or
    0AB0: key_pressed 3@
    0AB0: key_pressed 4@
else_jump @TimeCircuitsKeyReleased
jump @TimeCircuitsKeyHold

:TimeCircuitsKeyReleased
3F15: move_car_part "tcdkeypadbutton" index 2@ pos 0.0 0.0 0.0 car 0@
return

// =========================================================================================

:ParseDate
0085: 5@ = 7@
5@ /= 1000000
5@ *= 1000000
0062: 7@ -= 5@
5@ /= 1000000

0085: 6@ = 7@
6@ /= 10000
6@ *= 10000
0062: 7@ -= 6@
6@ /= 10000

// Check month
if or
    5@ < 1
    5@ > 12
then
    10@ = 1
    return
end

// Check Day
if
    6@ < 1
then
    10@ = 1
    return
end

// Months have different amount of days
if
    5@ == 2
then
    // Leap year check
    0B14: 11@ = 8@ MOD 4
    0B14: 12@ = 8@ MOD 100
    0B14: 13@ = 8@ MOD 400
    if and
        11@ == 0
        not 12@ == 0
    then
        if
            6@ > 29
        then
            10@ = 1
            return
        end
    else if
        13@ == 0
    then
        if
            6@ > 29
        then
            10@ = 1
            return
        end
    else
        if
            6@ > 28
        then
            10@ = 1
            return
        end
    end
    end
else if or
    5@ == 4
    5@ == 6
    5@ == 9
    5@ == 11
then
    if
        6@ > 30
    then
        10@ = 1
        return
    end
else
    if
        6@ > 31
    then
        10@ = 1
        return
    end
end
end
return

:ParseTime
0085: 8@ = 9@
8@ /= 100
8@ *= 100
0062: 9@ -= 8@
8@ /= 100

// Check Time
if
    8@ > 23
then
    10@ = 1
end

if
    9@ > 59
then
    10@ = 1
end
return

// =========================================================================================

:TimeCircuitsKeypadConfirm
10@ = 0
if and
    not 14@ == 0
    15@ == 0
then
    end_thread_named 'TC_LIT'
    create_thread @TimeCircuitsConfirmLight 0@
    // Parse dates and check for errors
    if
        1@ == 4
    then
        0085: 9@ = 7@
        gosub @ParseTime
    else if
        1@ == 8
    then
        gosub @ParseDate
    else if
        1@ == 12
    then
        gosub @ParseDate
        gosub @ParseTime
    else
        10@ = 1
    end
    end
    end

    // Handle errors
    if
        10@ == 0
    then
        3F84: play_sound "enter.wav" loop 0

        if or
            1@ == 12
            1@ == 8
        then
            $FLASH = 0xFF
            008A: $DTIME = 7@ // integer values and handles
            $DTIME *= 100
            005E: $DTIME += 5@
            $DTIME *= 100
            005E: $DTIME += 6@
            //01E5: text_1number_highpriority 'SPEED' $DTIME time 5000 1  // Cost: $~1~
        end

        if or
            1@ == 12
            1@ == 4
        then
            008A: $DTIME1 = 8@ // integer values and handles
            $DTIME1 *= 100
            005E: $DTIME1 += 9@
        end
    else
        3F84: play_sound "error.wav" loop 0
    end
    1@ = 0
    7@ = 0
    9@ = 0
end
3F14: move_car_part "tcdkeypadbuttonenter" pos 0.0 0.005 0.0 car 0@

:TimeCircuitsKeypadConfirmHold
wait 10
if or
    0AB0: key_pressed 0x6D
    0AB0: key_pressed 0xBD
else_jump @TimeCircuitsKeypadConfirmRelease
jump @TimeCircuitsKeypadConfirmHold

:TimeCircuitsKeypadConfirmRelease
3F14: move_car_part "tcdkeypadbuttonenter" pos 0.0 0.0 0.0 car 0@
return

:TimeCircuitsConfirmLight
thread 'TC_LIT'
3F10: set_car 0@ component "tcdkeypadenterlighton" visible 1
wait 450
3F10: set_car 0@ component "tcdkeypadenterlighton" visible 0
end_thread