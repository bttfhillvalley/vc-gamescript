:StatusIndicatorDisplay
// 0@ = SID Column
// 1@ = Car
// 2@ = SID Current
// 3@ = SID Target
// 16@ = Speed of lines
// 17@ = Target change rate
$DEBUG_SPEED = 0.0

:SIDStart
wait 10
if
   not Car.Wrecked($DELOREAN)
else_jump @SIDStart
008B: 1@ = $DELOREAN // integer values and handles
for 5@ = 0 to 20 step 1
    0085: 4@ = 5@
    if
        0@ == 10
    then
        4@ *= 10
    end
    4@ *= 10
    0058: 4@ += 0@
    3F11: set_car 1@ component "sidledsline" index 4@ visible 0
    3F19: set_car 1@ component "sidledsline" index 4@ glow 1
end
02E3: 11@ = car 1@ speed
// Set target
if or
    $TCON == 0 // integer values
    $FRIED == 1 // integer values
    $COOLDOWN == 1
then
    3@ = 0
else if
    11@ >= 46.5 // 85.3
then
    3@ = 20
else if
    11@ >= 43.5
then
    if
        0@ == 3
    then
        3@ = 13
    else if
        0@ == 8
    then
        3@ = 10
    else if
        0@ == 10
    then
        3@ = 18
    else
        3@ = 20
    end
    end
    end
else if
    11@ >= 40.5 // 74.3 mph, subject to change
then
    if
        0@ == 1
    then
        3@ = 19
    else if
        0@ == 3
    then
        3@ = 6
    else if or
        0@ == 5
        0@ == 8
    then
        3@ = 0
    else if
        0@ == 6
    then
        3@ = 12
    else if
        0@ == 9
    then
        3@ = 20
    else
        3@ = 10
    end
    end
    end
    end
    end
else
    0086: 3@ = 11@
    3@ *= 1.835
    if
        0@ == 1
    then
        3@ /= 5.0
    else if
        0@ == 3
    then
        3@ /= 16.0
    else if or
        0@ == 5
        0@ == 8
    then
        3@ = 0.0
    else if
        0@ == 6
    then
        3@ /= 8.0
    else if
        0@ == 9
    then
        3@ /= 4.0
    else
        3@ /= 3.0
        if
            3@ > 10.0
        then
            3@ = 10.0
        end
    end
    end
    end
    end
    end
    008C: 3@ = float_to_integer 3@
end
end
end
end

:SIDMove
if
    16@ > 25
else_jump @SIDShow
16@ = 0
if
    001C: 3@ > 2@
then
    2@ += 1
else if
    001C: 2@ > 3@
then
    2@ -= 1
end
end

:SIDShow
for 5@ = 0 to 2@ step 1
    0085: 4@ = 5@
    if
        0@ == 10
    then
        4@ *= 10
    end
    4@ *= 10
    0058: 4@ += 0@
    3F11: set_car 1@ component "sidledsline" index 4@ visible 1
end
jump @SIDStart