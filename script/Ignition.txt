:Ignition
thread 'IGNTN'

// TODO:  This startup sequence needs to be redone
// The oil pressure needle moves, but not the oil temp
// Volts needle is oscillating too much

:IgnitionStart
wait 10
if
   Player.Defined($PLAYER_CHAR)
else_jump @IgnitionStart
if
00DE:   player $PLAYER_CHAR driving_vehicle_type #INFERNUS //BTTF1
else_jump @IgnitionStart
03C1: 0@ = player $PLAYER_CHAR car
040D: unload_wav 2
03CF: load_wav 'BURG_02' as 2
create_thread @PlaySound 2

//turn key
wait 0
// Initialize dashboard
3F16: rotate_car_part "ignitionkey" angle 135.0 0.0 0.0 car 0@
3F16: rotate_car_part "ignition" angle 135.0 0.0 0.0 car 0@
3F16: rotate_car_part "oilneedle" angle 0.0 -30.0 0.0 car 0@  //warm main temp
3F10: set_car 0@ component "lambdalight" visible 0 //keep off
3F10: set_car 0@ component "fuellight" visible 0   //keep off
//3F10: set_car 0@ component "doorajarlight" visible 1 //activate
3F10: set_car 0@ component "oillight" visible 1     //activate
3F10: set_car 0@ component "brakelight" visible 1    //activate
3F10: set_car 0@ component "batterylight" visible 1  //activate

// Wiggle volts needle
for 2@ = 0 to 3 step 1
    for 1@ = 80.0 downto 0.0 step 20.0
        wait 10
        3F16: rotate_car_part "voltsneedle" angle 0.0 1@ 0.0 car 0@
    end
    // Down
    for 1@ = 0.0 to 80.0 step 20.0
        wait 10
        3F16: rotate_car_part "voltsneedle" angle 0.0 1@ 0.0 car 0@
    end
end

// Center Volts
3F16: rotate_car_part "voltsneedle" angle 0.0 40.0 0.0 car 0@

// Warm up oil
wait 1000
for 1@ = -30.0 to 0.0 step 5.0
    wait 10
    3F16: rotate_car_part "oilneedle" angle 0.0 1@ 0.0 car 0@
end

// Oil Temp Diagnostic (?)
wait 500
for 1@ = 0.0 downto -75.0 step 5.0
    wait 10
    3F16: rotate_car_part "oilneedle" angle 0.0 1@ 0.0 car 0@
end

// Turn off Dashboard lights
wait 1000
3F10: set_car 0@ component "batterylight" visible 0  //off
wait 1000
3F10: set_car 0@ component "oillight" visible 0    //off
wait 1000
3F10: set_car 0@ component "seatbeltlight" visible 1
wait 1000
3F10: set_car 0@ component "seatbeltlight" visible 0
wait 5000

// Stabilize oil
for 1@ = -75.0 to 30 step 5.0
    wait 10
    3F16: rotate_car_part "oilneedle" angle 0.0 1@ 0.0 car 0@
end


:IgnitionWait
wait 10
if and
8119:   not car 0@ wrecked
00DC:   player $PLAYER_CHAR driving 0@
else_jump @IgnitionStart
jump @IgnitionWait
