:TimeCircuitsFlash
0B10: 1@ = 0@ AND $FLASH_BIT
if
    1@ == 0
then
    wait 450
    $FLASH = 0x8
    wait 100
    $FLASH = 0
else
    wait 450
    $FLASH = 0x7
    wait 50
    $FLASH = 0x3
    wait 50
    $FLASH = 0
end
end_thread

:TimeCircuits
thread 'T_CIRC'

:TimeCircuitsStart
wait 10
if
   Player.Defined($PLAYER_CHAR)
jf @TimeCircuitsStart
if
00DC:   player $PLAYER_CHAR driving $DELOREAN
jf @TimeCircuitsStart
if
  $TCON == 1 // integer values
jf @TimeCircuitsStart
if and
  $TRAVEL == 0 // integer values
  $CUTSCENE == 0 // integer values
jf @TimeCircuitsStart
if
  $FRIED == 0 // integer values
jf @TimeCircuitsStart
if
  $TIME_DISP == 1
jf @TimeCircuitsStart
// Draw Time Circuit Display Image
05F5: call_scm_func @DrawTexture params_count 9 1 733.0 242.0 216.0 128.0 255 255 255 255

// Handle colon timer
if
    16@ > 999 // integer values
then
    16@ = 0 // integer values
end


// Draw Destination Time
0@ = 200.0 // floating-point values
1@ = 255 // integer values
2@ = 80 // integer values
3@ = 30 // integer values
008B: 4@ = $MONTH // integer values and handles
008B: 5@ = $DAY // integer values and handles
008B: 6@ = $YEAR // integer values and handles
008B: 7@ = $HOUR // integer values and handles
008B: 8@ = $MINUTE // integer values and handles

// Handle flash
0B10: 15@ = $FLASH AND 0x8
if
    15@ == 0
then
    05F5: call_scm_func @TimeCircuitsMonth params_count 5 0@ 1@ 2@ 3@ 4@
end
0B10: 15@ = $FLASH AND 0x4
if
    15@ == 0
then
    05F5: call_scm_func @TimeCircuitsDay params_count 5 0@ 1@ 2@ 3@ 5@
end
0B10: 15@ = $FLASH AND 0x2
if
    15@ == 0
then
    05F5: call_scm_func @TimeCircuitsYear params_count 5 0@ 1@ 2@ 3@ 6@
end
0B10: 15@ = $FLASH AND 0x1
if
    15@ == 0
then
    05F5: call_scm_func @TimeCircuitsHour params_count 5 0@ 1@ 2@ 3@ 7@
    05F5: call_scm_func @TimeCircuitsMinute params_count 5 0@ 1@ 2@ 3@ 8@
    05F5: call_scm_func @TimeCircuitsDots params_count 6 0@ 1@ 2@ 3@ 7@ 16@
end

// Draw Present Time
0@ += 43.0 // floating-point values
1@ = 70 // integer values
2@ = 240 // integer values
3@ = 10 // integer values
008B: 4@ = $CMONTH // integer values and handles
008B: 5@ = $CDAY // integer values and handles
008B: 6@ = $CYEAR // integer values and handles
00BF: 7@ = current_time_hours, 8@ = current_time_minutes
gosub @TimeCircuitsDisplay

// Draw Last Time Departed
0@ += 43.0 // floating-point values
1@ = 240 // integer values
2@ = 190 // integer values
3@ = 40 // integer values
008B: 4@ = $PTIME
if
   not 4@ == -1 // integer values
then
    008B: 5@ = $PTIME // integer values and handles
    008B: 6@ = $PTIME
    4@ /= 100
    6@ /= 100
    6@ *= 100
    0062: 5@ -= 6@
    6@ /= 10000
    6@ *= 100
    0062: 4@ -= 6@
    6@ /= 100
    008B: 7@ = $PTIME1 // integer values and handles
    008B: 8@ = $PTIME1 // integer values and handles
    7@ /= 100
    7@ *= 100
    0062: 8@ -= 7@
    7@ /= 100
else
    4@ = 0
    5@ = -1
    6@ = -1
    7@ = -1
    8@ = -1
end
gosub @TimeCircuitsDisplay

:TimeCircuitsDamage
if
8185:   not car $DELOREAN health >= 650
jf @TimeCircuitsStart
05F5: call_scm_func @DrawTexture params_count 9 6 735.0 155.0 45.0 23.0 255 255 255 255 //lambda error light
3F10: set_car $DELOREAN component "lambdalight" visible 1
jump @TimeCircuitsStart

:DivideTwo
if
    4@ >= 0
then
    0085: 5@ = 4@
    4@ /= 10
    4@ *= 10
    0062: 5@ -= 4@
    4@ /= 10
else
    5@ = -1
end
return

:DivideFour
if
    4@ >= 0
then
    0085: 5@ = 4@
    5@ /= 100
    0085: 6@ = 4@
    6@ /= 10
    0085: 7@ = 4@
    4@ /= 10
    4@ *= 10
    0062: 7@ -= 4@
    4@ /= 100
    4@ *= 10
    0062: 6@ -= 4@
    4@ /= 100
    4@ *= 10
    0062: 5@ -= 4@
    4@ /= 10
else
    5@ = -1
    6@ = -1
    7@ = -1
end
return

:TimeCircuitsTextureOffset
// TXD Offsets
4@ += 30
5@ += 30
6@ += 30
7@ += 30
return

:TimeCircuitsDisplay
05F5: call_scm_func @TimeCircuitsMonth params_count 5 0@ 1@ 2@ 3@ 4@
05F5: call_scm_func @TimeCircuitsDay params_count 5 0@ 1@ 2@ 3@ 5@
05F5: call_scm_func @TimeCircuitsYear params_count 5 0@ 1@ 2@ 3@ 6@
05F5: call_scm_func @TimeCircuitsHour params_count 5 0@ 1@ 2@ 3@ 7@
05F5: call_scm_func @TimeCircuitsMinute params_count 5 0@ 1@ 2@ 3@ 8@
05F5: call_scm_func @TimeCircuitsDots params_count 6 0@ 1@ 2@ 3@ 7@ 16@
return

:TimeCircuitsMonth
4@ += 41
05F5: call_scm_func @DrawTexture params_count 9 4@ 650.0 0@ 41.0 18.0 1@ 2@ 3@ 255
05F6: cleo_return 0

:TimeCircuitsDay
gosub @DivideTwo
gosub @TimeCircuitsTextureOffset
05F5: call_scm_func @DrawTexture params_count 9 4@ 680.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 5@ 692.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F6: cleo_return 0

:TimeCircuitsYear
gosub @DivideFour
gosub @TimeCircuitsTextureOffset
05F5: call_scm_func @DrawTexture params_count 9 4@ 716.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 5@ 728.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 6@ 740.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 7@ 752.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F6: cleo_return 0

:TimeCircuitsHourAdjust
if
    4@ > 12
then
    4@ -= 12
end

if
    4@ == 0
then
    4@ = 12
end
return

:TimeCircuitsHour
gosub @TimeCircuitsHourAdjust
gosub @DivideTwo
gosub @TimeCircuitsTextureOffset
05F5: call_scm_func @DrawTexture params_count 9 4@ 788.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 5@ 800.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F6: cleo_return 0


:TimeCircuitsMinute
gosub @DivideTwo
gosub @TimeCircuitsTextureOffset
05F5: call_scm_func @DrawTexture params_count 9 4@ 824.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F5: call_scm_func @DrawTexture params_count 9 5@ 836.0 0@ 12.0 18.0 1@ 2@ 3@ 255
05F6: cleo_return 0

:TimeCircuitsDots
// Red Destination dots are Amber
if
    1@ == 255
then
    2@ = 255
end

// AM/PM Dot
if
    4@ < 12
then
    0@ -= 6.0
    05F5: call_scm_func @DrawTexture params_count 9 54 771.0 0@ 6.0 8.0 1@ 2@ 3@ 255
    0@ += 6.0
else
    0@ += 5.0
    05F5: call_scm_func @DrawTexture params_count 9 54 771.0 0@ 6.0 8.0 1@ 2@ 3@ 255
    0@ -= 5.0
end

// Flash Colon
if
    5@ < 500 // integer values
then
    0@ -= 2.0
    05F5: call_scm_func @DrawTexture params_count 9 54 812.5 0@ 6.0 8.0 1@ 2@ 3@ 255
    0@ += 6.0
    05F5: call_scm_func @DrawTexture params_count 9 54 812.5 0@ 6.0 8.0 1@ 2@ 3@ 255
    0@ -= 4.0
end
05F6: cleo_return 0
