// 0@: Dummy Car
// 1@: Dummy Actor
// 15@: Camera mode  // 15 For tracking, 3 for Behind the car
:RadioControl
thread 'R_CONT'
15@ = 15 // integer values

:RadioControlStart
wait 10
if and
   Player.Defined($PLAYER_CHAR)
    80E0:   not player $PLAYER_CHAR driving
    not Car.Wrecked($DELOREAN)
    003A:   $KEY == $KEY_RADIOCONTROL // R
else_jump @RadioControlStart
$IN_RC = 1 // integer values
Model.Load(#SABRE) //DMC-12
Model.Load(#SCREWDRIVER) //RC REMOTE
038B: load_requested_models

:RadioControlLoadModels                                   
wait 0
if and
   Model.Available(#SABRE) //DMC-12
   Model.Available(#SCREWDRIVER) //RC REMOTE
else_jump @RadioControlLoadModels

// Set camera first
04C4: create_coordinate 2@ 3@ 4@ from_actor $PLAYER_ACTOR offset 0.0 0.0 1.0
Camera.SetPosition(2@, 3@, 4@, 0.0, 0.0, 0.0)
Camera.OnVehicle($DELOREAN, 15@, 1)

// Create Dummy car
04C4: create_coordinate 2@ 3@ 4@ from_actor $PLAYER_ACTOR offset 0.0 0.0 -1.0
0@ = Car.Create(#SABRE, 2@, 3@, 5000.0) //DMC-12
0369: put_player $PLAYER_CHAR in_car 0@
02D4: car 0@ turn_off_engine

// Create Dummy Actor
5@ = Actor.Angle($PLAYER_ACTOR)
0225: 6@ = player $PLAYER_CHAR health
009A: 1@ = create_actor_pedtype 4 model #NULL at 2@ 3@ 4@ //player model
Actor.Angle(1@) = 5@
Actor.Health(1@) = 6@
01B2: give_actor 1@ weapon 34 ammo 1 // Load the weapon model before using this
04C4: create_coordinate 2@ 3@ 4@ from_actor 1@ offset 0.0 -1.0 0.0
0245: set_actor 1@ walk_style_to 33

// Set initial Angle
0407: create_coordinate 2@ 3@ 4@ from_car $DELOREAN offset 0.0 0.0 -0.75
01BE: set_actor 1@ to_look_at_spot 2@ 3@ 4@


// Set Remote mode
3F03: car $DELOREAN turn_on_engine
3F23: set_car $DELOREAN remote
04D6: enable_rc_car_detonation 0
048A: enable_rc_vehicle_detonation 0

:RadioControlLoop
wait 10
if and
    3F29:   is_player_in_remote_mode_with_car $DELOREAN
    not Car.Wrecked($DELOREAN)
    803A:   $KEY == $KEY_RADIOCONTROL // R
else_jump @RadioControlExit
if and
    00E1:   player 0 pressed_button 13
    16@ > 500
then
    16@ = 0
    if
        15@ == 3
    then
        15@ = 15
    else
        15@ = 3
    end
end

// Keep track of Delorean and have the dummy car over it to keep cars loaded
0407: create_coordinate 2@ 3@ 4@ from_car $DELOREAN offset 0.0 0.0 -0.75
Car.PutAt(0@, 2@, 3@, 5000.0)
01BE: set_actor 1@ to_look_at_spot 2@ 3@ 4@
04C4: create_coordinate 2@ 3@ 4@ from_actor 1@ offset 0.0 0.0 1.0
Camera.SetPosition(2@, 3@, 4@, 0.0, 0.0, 0.0)
Camera.OnVehicle($DELOREAN, 15@, 2)
jump @RadioControlLoop

:RadioControlExit
$IN_RC = 0 // integer values
3F24: remove_car $DELOREAN remote
04C4: create_coordinate 2@ 3@ 4@ from_actor 1@ offset 0.0 0.0 -1.0
5@ = Actor.Angle(1@)
6@ = Actor.Health(1@)
Actor.DestroyInstantly(1@)
012A: put_player $PLAYER_CHAR at 2@ 3@ 4@ and_remove_from_car
Actor.Angle($PLAYER_ACTOR) = 5@
Actor.Health($PLAYER_ACTOR) = 6@
Car.Destroy(0@)
if
    15@ == 3
then
    Camera.Restore_WithJumpCut
else
    Camera.Restore
end
Camera.SetBehindPlayer
Player.CanMove($PLAYER_CHAR) = False
wait 500
Player.CanMove($PLAYER_CHAR) = True
jump @RadioControlStart